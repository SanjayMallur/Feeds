apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'


android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.smallur.feeds"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug{
            buildConfigField 'String', 'BASE_URL', '"https://realserver.typicode.com/"'
        }
        release {
            buildConfigField 'String', 'BASE_URL', '"https://jsonplaceholder.typicode.com/"'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "default"
    productFlavors {
        mock {
            dimension "default"
            applicationIdSuffix = ".mock"
        }
        prod {
            dimension "default"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }
    dataBinding {
        enabled = true
    }
}

subprojects {
    configurations.all {
        resolutionStrategy {
            forcedModules = [
                    "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.kotlinStdLibVersion",
                    "org.jetbrains.kotlin:kotlin-reflect:$rootProject.kotlinStdLibVersion"
            ]
        }
    }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    // Internal modules
    implementation project(':base-library-android')

    // Android Testing Support Library's runner
    androidTestImplementation "com.android.support.test:runner: $rootProject.runnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.espressoCoreVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$rootProject.espressoCoreVersion", {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

    // Dagger 2
    implementation 'com.google.dagger:dagger:2.14.1'
    kapt "com.google.dagger:dagger-compiler:2.14.1"
    compileOnly 'org.glassfish:javax.annotation:3.1.1'
    // Dagger Android
    kapt "com.google.dagger:dagger-android-processor:2.14.1"
    implementation 'com.google.dagger:dagger-android:2.14.1'
    implementation 'com.google.dagger:dagger-android-support:2.14.1'

    //Retrofit 2
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation "com.squareup.retrofit2:converter-gson:2.3.0"

    implementation 'de.hdodenhof:circleimageview:2.1.0'

    //Kotlin testing
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$rootProject.kotlinVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoCoreVersion"
    testImplementation "org.assertj:assertj-core:$rootProject.assertJCoreVersion"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$rootProject.mockitoKotlinSyntaxVersion"
    androidTestImplementation("com.nhaarman:mockito-kotlin-kt1.1:$rootProject.mockitoKotlinSyntaxVersion", {
        exclude group: 'org.mockito', module: 'mockito-core'
    })
    androidTestImplementation "org.mockito:mockito-android:2.11.0"
}
